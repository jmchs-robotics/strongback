plugins {
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'java'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
    maven {
        name = 'WPI'
        url = 'http://first.wpi.edu/FRC/roborio/maven/release'
    }
}

dependencies {
    compile 'edu.wpi.first.wpilib.networktables.java:NetworkTables:+:desktop'
    compile 'edu.wpi.first.wpilib.networktables.java:NetworkTables:+:arm'

    compile 'edu.wpi.first.wpilibj:wpilibJavaFinal:+'

    compile 'junit:junit:4.11'
    compile 'io.dropwizard.metrics:metrics-core:3.1.0'

    testCompile 'org.easytesting:fest-assert:1.4'
}

version = '1.1.7'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'org.strongback'
            artifactId 'strongback'
            version project.version
        }
    }
}

bintray {
    user = bintray_user //this usually comes from gradle.properties file in ~/.gradle
    key = bintray_api_key //this usually comes from gradle.properties file in ~/.gradle
    publications = ['mavenJava']
    pkg {
        repo = 'utilities'
        name = 'strongback'
        userOrg = 'judgementcall5933'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/jmchs-robotics/strongback'
        version {
            name = project.version
        }
    }
}
